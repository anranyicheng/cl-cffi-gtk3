;;;; Example Statusbar - 2022-12-22

(in-package :gtk3-example)

(defun example-statusbar (&optional application)
  (within-main-loop
    (let* ((window (make-instance 'gtk:window
                                  :type :toplevel
                                  :title "Example Statusbar"
                                  :application application
                                  :default-width 300
                                  :border-width 12))
           (vbox (make-instance 'gtk:box
                                :orientation :vertical
                                :homogeneous nil
                                :spacing 3))
           (statusbar (make-instance 'gtk:statusbar))
           (id (gtk:statusbar-context-id statusbar "Example Statusbar"))
           (count 0))
      (g:signal-connect window "destroy"
                        (lambda (widget)
                          (declare (ignore widget))
                          (leave-gtk-main)))
      (gtk:box-pack-start vbox statusbar)
      (let ((button (gtk:button-new-with-label "Push Item")))
        (g:signal-connect button "clicked"
           (lambda (widget)
             (declare (ignore widget))
             (setq count (+ 1 count))
             (gtk:statusbar-push statusbar id (format nil "Item ~A" count))))
        (gtk:box-pack-start vbox button :expand t :fill t :padding 3))
      (let ((button (gtk:button-new-with-label "Pop Item")))
        (g:signal-connect button "clicked"
           (lambda (widget)
             (declare (ignore widget))
             (gtk:statusbar-pop statusbar id)))
        (gtk:box-pack-start vbox button :expand t :fill t :padding 3))
      (gtk:container-add window vbox)
      (gtk:widget-show-all window))))
